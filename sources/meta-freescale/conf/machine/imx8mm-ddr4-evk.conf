#@TYPE: Machine
#@NAME: NXP i.MX 8M Mini EVK with DDR4
#@SOC: i.MX8MM
#@DESCRIPTION: Machine configuration for NXP i.MX 8M Mini Evaluation Kit with DDR4
#@MAINTAINER: Jun Zhu <junzhu@nxp.com>

require include/imx8mm-evk.inc

KERNEL_DEVICETREE_BASENAME = "${MACHINE}"
KERNEL_DEVICETREE:append:use-nxp-bsp = " \
    freescale/${KERNEL_DEVICETREE_BASENAME}-revb-rm67191.dtb \
    freescale/${KERNEL_DEVICETREE_BASENAME}-revb.dtb \
"

UBOOT_CONFIG_BASENAME = "imx8mm_ddr4_evk"
UBOOT_CONFIG[nand] = "${UBOOT_CONFIG_BASENAME}_nand_defconfig,ubifs"

DDR_FIRMWARE_NAME = " \
    ddr4_imem_1d.bin \
    ddr4_dmem_1d.bin \
    ddr4_imem_2d.bin \
    ddr4_dmem_2d.bin \
"

IMXBOOT_TARGETS_BASENAME = "flash_ddr4_evk"

# Mainline BSP doesn't support DDR4 so it must be set to nxp.
# Also this machine isn't supported by u-boot-fslc but imx8mm-evk.inc already
# set the bootloader to u-boot-imx instead when NXP BSP is used.
IMX_DEFAULT_BSP = "nxp"
