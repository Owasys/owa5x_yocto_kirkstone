#@TYPE: Machine
#@NAME: NXP i.MX 8MNUL EVK with DDR3L
#@SOC: i.MX8MN
#@DESCRIPTION: Machine configuration for NXP i.MX 8M Nano UltraLite Evaluation Kit with DDR3L
#@MAINTAINER: Yuqing Zhu <carol.zhu@nxp.com>

MACHINEOVERRIDES =. "mx8:mx8m:mx8mnul:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/armv8a/tune-cortexa53.inc

MACHINE_FEATURES += " wifi bluetooth bcm43455 bcm4356 bcm4359 optee"

KERNEL_DEVICETREE = " \
    freescale/imx8mn-ddr3l-evk.dtb \
    freescale/imx8mn-ddr3l-evk-ak5558.dtb \
    freescale/imx8mn-ddr3l-evk-rpmsg.dtb \
"

WKS_FILE_DEPENDS:append = " imx-m7-demos"
IMAGE_BOOT_FILES += " \
    imx8mnddr3l_m7_TCM_hello_world.bin \
    imx8mnddr3l_m7_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mnddr3l_m7_TCM_rpmsg_lite_str_echo_rtos.bin \
    imx8mnddr3l_m7_TCM_sai_low_power_audio.bin \
"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "imx8mn_ddr3l_evk_config,sdcard"
UBOOT_CONFIG[fspi] = "imx8mn_ddr3l_evk_defconfig"

SPL_BINARY = "spl/u-boot-spl.bin"

ATF_PLATFORM = "imx8mn"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = " \
    ddr3_dmem_1d_201810.bin \
    ddr3_imem_1d_201810.bin \
"

# Set u-boot DTB
UBOOT_DTB_NAME = "imx8mn-ddr3l-evk.dtb"

# Set imx-mkimage boot target
IMXBOOT_TARGETS = "${@bb.utils.contains('UBOOT_CONFIG', 'fspi', 'flash_ddr3l_evk_flexspi', \
                                                                'flash_ddr3l_evk', d)}"

IMX_BOOT_SOC_TARGET = "iMX8MN"

SERIAL_CONSOLES = "115200;ttymxc1"

IMAGE_BOOTLOADER = "imx-boot"

BOOT_SPACE = "65536"

LOADADDR = ""
UBOOT_SUFFIX:pn-u-boot-imx = "bin"
UBOOT_MAKE_TARGET:pn-u-boot-imx = ""
IMX_BOOT_SEEK = "32"
